apply plugin: "kotlin"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply plugin: "org.jetbrains.dokka"

group 'com.octo.kotlinelements'
version kotlinElementsVersion

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion" // Force transitive kotlinpoet version
    compile ("com.squareup:kotlinpoet:0.7.0") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jre7'
    }

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.10.0"
    testCompile "org.mockito:mockito-core:2.19.0"
    testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    testCompile "com.google.testing.compile:compile-testing:0.15"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


publishing {
    publications {
        kotlinElements(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                asNode().with {
                    appendNode("description", "A set of extensions over the `javax.lang.model.element` API and KotlinPoet `TypeName` to facilitate analysing and writing Kotlin code in annotation processing tools.")
                    appendNode("licenses").with {
                        appendNode("license").with {
                            appendNode("name", "MIT License")
                            appendNode("url", "https://opensource.org/licenses/MIT")
                        }
                    }
                    appendNode("organization").with {
                        appendNode("name", "OCTO Technology â€“ Mobile Squad")
                        appendNode("url", "https://www.octo.com")
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ["kotlinElements"]
    pkg {
        repo = bintrayRepository
        name = project.name
        userOrg = "octomob"
        licenses = ["MIT"]
        vcsUrl = "git@github.com:octo-technology/worker-decorator.git"
        version {
            name = project.version
        }
    }
    publish = true
}