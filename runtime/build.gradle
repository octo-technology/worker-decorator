apply plugin: "kotlin"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply plugin: "org.jetbrains.dokka"

sourceCompatibility = 1.7

dependencies {
    compile "org.jetbrains:annotations-java5:16.0.2"
}


task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        workerDecoratorRuntime(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                asNode().with {
                    appendNode("description", "An annotation processor generating various decorations for supplied interfaces.")
                    appendNode("licenses").with {
                        appendNode("license").with {
                            appendNode("name", "MIT License")
                            appendNode("url", "hhttps://opensource.org/licenses/MIT")
                        }
                    }
                    appendNode("organization").with {
                        appendNode("name", "OCTO Technology â€“ Mobile Squad")
                        appendNode("url", "https://www.octo.com")
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ["workerDecoratorRuntime"]
    pkg {
        repo = bintrayRepository
        name = project.name
        userOrg = "octomob"
        licenses = ["MIT"]
        vcsUrl = "git@github.com:octo-technology/worker-decorator.git"
        version {
            name = project.version
        }
    }
    publish = true
}